#summary BNF grammar for QBQL

----

= QBQL Grammar =

{{{
assertion:   boolean "."
          |  implication "."
assignment:  identifier "=" expr ";"
attribute:   identifier
boolean:     "(" boolean ")"
          |  boolean "&" boolean
          |  expr "<" expr
          |  expr "=" expr
          |  expr "~" expr
complement:  expr "'"
database:    assignment
          |  database assignment
expr:        complement
          |  identifier
          |  innerJoin
          |  innerUnion
          |  join
          |  outerUnion
          |  relation
          |  unison
          |  "(" expr ")"
implication:  boolean "-" ">" boolean
innerJoin:   expr "*" expr
innerUnion:  expr "v" expr
join:        expr "^" expr
outerUnion:  expr "+" expr
program:     assertion
          |  assignment
          |  query
          |  program program
query:       expr ";"
relation:    "{" tuples "}"
tuple:       "<" values ">"
tuples:      tuple
          |  tuples "," tuple
unison:      expr "@" expr
value:       attribute "=" digits
          |  attribute "=" identifier
values:      value
          |  values "," value
}}}