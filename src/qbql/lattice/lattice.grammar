almostDisj:  expr '/' '1' expr
;

assertion:   implication '.'
;

assignment:  identifier '=' expr ';'
          |  string_literal '=' expr ';'
          |  identifier '=' partition ';'
;

attribute:   identifier
;

big:         expr '/' '!' expr
;

CPclosure: identifier '*'
      |  parExpr '*'
;

composition: '/' '^'
;

conjunction: 
             conjunction '&' conjunction
          |  proposition 
          |  '(' conjunction ')'
          |   '-' '(' conjunction ')'
;

complement:  identifier 'qtSymbol'
          |  parExpr 'qtSymbol'
;

contains:     expr '/' '<' expr
;

content:     value
          |  content value
;

count:   expr '|' 'v' '|' expr
;

EQclosure: identifier '+'
        |  parExpr '+'
;


database:    assignment
          |  database assignment
;

disjoint:    expr '/' '0' expr
;

disjunction: 
             conjunction '|' disjunction
          |  conjunction 
          |  '(' disjunction ')'
;

expr:        almostDisj
          |  big
          |  complement
          |  contains
          |  count
          |  CPclosure
          |  disjoint
          |  EQclosure
          |  identifier
          |  string_literal
          |  innerUnion
          |  inverse
          |  join
          |  parExpr
          |  relation
          |  setEQ
          |  setIX
          |  table
          |  transpCont
          |  unnamedJoin
          |  unnamedMeet
          |  userDefined
;

filename:
           identifier
         | identifier '.' identifier
         | string_literal
;

header:      identifier
          |  string_literal
          |  header identifier
          |  header ',' identifier
;

implication: 
             implication '-' '>' implication
          |  implication '<' '-' implication
          |  implication '<' '-' '>' implication
          |  disjunction
          |  '(' implication ')'
          |  inductionFormula
;

inductionFormula: identifier '(' parameters ')'    -- e.g. find minimal formula satisfying Armstrong axioms, such as FD(r,x,y) introduced at http://arxiv.org/pdf/0902.3532v2
;

innerUnion:  expr 'v' expr
;

inverse:     identifier '`'
          |  parExpr '`'
;

include: 'include' filename ';'
;

join:        expr '^' expr
;

oper:       '^'
          | 'v'
          | userOper
;

parameters: expr
          | parameters ',' expr
;

parExpr:     '(' expr ')'
;

partition:  
            '(' partition ')'
          |  expr '#' expr
          |  partition '^' partition
          |  partition 'v' partition
          
          |  --partComp   
             partition composition partition   -- partially defined:
                                           -- result may be not an equivalence relation
          
          --|  content
          --|  partition '|' content   
             
          |  tuple
          |  partition '|' tuple      
;

proposition:     
             "not to confuse with userOper"
          |  implication
          |  expr '=' expr
          |  expr '~' expr
          |  partition '<' partition
          |  partition '=' partition
          |  partition '>' partition
          |  expr '!' '=' expr
          |  expr '<' '~' expr
          |  expr '>' '~' expr
          |  oper '!' '=' oper
;

program:   stmt  
          |  stmt program
;

query:       expr ';'
          |  partition ';'
;

setEQ:       expr '/' '=' expr
;

setIX:       expr composition expr
;

stmt:        assertion
          |  assignment
          |  query
          |  include
;

table:       '[' ']'
          |  '[' header ']'
          |  '[' header ']' content
/*        |  '[' identifier '=' identifier ']'  - deprecated. Use string literals, e.g. "x=y"*/
;

transpCont:  expr '/' '>' expr
;
unnamedJoin:  expr '^' '~' expr
;
unnamedMeet:  expr 'v' '~' expr
;

unaryUserDefined:
              userOper parExpr 
           |  userOper unaryUserDefined 
           |  userOper identifier 
;
userDefined:  
			  unaryUserDefined   -- prefix
           |  expr userOper expr -- infix
;
userOper:  '<' identifier '>'
          | '<' string_literal '>'
;
value:       digits
          |  '-' digits
          |  identifier
          |  string_literal
          |  /* nested relation */ parExpr          
;

"not to confuse with userOper":
             expr '<' expr
          |  expr '>' expr
;

